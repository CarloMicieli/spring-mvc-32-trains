//!# /bin/gradle
group = "com.trenako"
description = "A social network for model railways."

def springVersion = "3.1.1.RELEASE"
def securityVersion = "3.1.0.RELEASE"
def slf4jVersion = "1.6.1"
def spockVersion = "0.6-groovy-1.8"

allprojects {
	apply plugin: 'java'
	apply plugin: 'groovy'
	apply plugin: 'eclipse-wtp'

	sourceCompatibility = JavaVersion.VERSION_1_6
	targetCompatibility = JavaVersion.VERSION_1_6

	repositories {
		mavenCentral()
		maven {url "http://maven.springframework.org/release"}
		maven {url "http://repository.jboss.org/nexus/content/repositories/releases/"}
	}

	configurations {
		integrationTestCompile {
			extendsFrom testCompile
		}
		integrationTestRuntime {
			extendsFrom integrationTestCompile, testRuntime
		}
	}

	sourceSets {
		integrationTest {
			groovy.srcDir file('src/integrationTest/groovy')
			resources.srcDir file('src/integrationTest/resources')
			compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.integrationTestCompile
			runtimeClasspath = output + compileClasspath + configurations.integrationTestRuntime
		}
	}

	eclipse {
		// add the integration classpath
		classpath {
			plusConfigurations += configurations.integrationTestRuntime

			//default settings for dependencies sources/javadoc download:
			downloadSources = true
			downloadJavadoc = false
		}
	}

	dependencies {
		// logging
		compile "org.slf4j:slf4j-api:${slf4jVersion}"
		runtime "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
			"org.slf4j:slf4j-log4j12:${slf4jVersion}",
			"log4j:log4j:1.2.16"

		// unit testing
		testCompile "junit:junit:4.10",
			"org.mockito:mockito-all:1.9.0",
			"org.springframework:spring-test:${springVersion}"

		// integration testing
		groovy "org.codehaus.groovy:groovy-all:1.8.6"
		integrationTestCompile "org.spockframework:spock-core:${spockVersion}",
			"org.spockframework:spock-spring:${spockVersion}"
	}

	// integration testing: the application is deployed to the web container
	task integrationTest(description: 'Runs the integration tests.', group: 'Verification', type: Test) {
		testClassesDir = sourceSets.integrationTest.output.classesDir
		classpath = sourceSets.integrationTest.runtimeClasspath
	}
	check.dependsOn integrationTest
}

subprojects { subproj ->
	jar {
		manifest.attributes['Implementation-Title'] = subproj.name
		manifest.attributes['Implementation-Version'] = subproj.version
	}
}

project("trenako-common") {
	description = 'The domain entity classes.'

	dependencies {
		// spring framework
		compile("org.springframework:spring-context:${springVersion}") {
			exclude group: "commons-logging", module: "commons-logging"
		}

		// logging
		compile "org.slf4j:slf4j-api:${slf4jVersion}"
		runtime "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
			"org.slf4j:slf4j-log4j12:${slf4jVersion}",
			"log4j:log4j:1.2.16"

		// GCLIB, required for @Configuration usage
		compile "cglib:cglib-nodep:2.2.2"

		// apache commons
		compile "org.apache.commons:commons-lang3:3.1"

		// JSR 303 with Hibernate Validator
		compile "javax.validation:validation-api:1.0.0.GA"
		compile "org.hibernate:hibernate-validator:4.2.0.Final"	

		// mongodb
		compile "org.springframework.data:spring-data-mongodb:1.0.1.RELEASE"
		compile "org.mongodb:mongo-java-driver:2.7.3"
	}
} 

project("trenako-services") {
	description = 'The domain entity classes.'

	dependencies {
		compile project(":trenako-common")

		// spring security
		compile("org.springframework.security:spring-security-core:${securityVersion}") {
			exclude group: "commons-logging", module: "commons-logging"
		}
		compile("org.springframework.security:spring-security-config:${securityVersion}") {
			exclude group: "commons-logging", module: "commons-logging"
		}
	}
}

project("trenako-web") {
	apply plugin: 'war'

	description = 'The mvc web application.'

	repositories {
		maven {url "http://maven.springframework.org/snapshot"}
	}

	dependencies {
		compile project(":trenako-common")
		compile project(":trenako-services")

		// spring mvc
		compile "org.springframework:spring-web:${springVersion}"
		compile "org.springframework:spring-webmvc:${springVersion}"
		
		compile("org.springframework.security:spring-security-web:${securityVersion}") {
			exclude group: "commons-logging", module: "commons-logging"
		}
		compile "org.springframework.security:spring-security-taglibs:${securityVersion}"

		// GCLIB, required for @Configuration usage
		compile "cglib:cglib-nodep:2.2.2"

		// Sitemesh
		compile "opensymphony:sitemesh:2.4.2"

		// mongodb
		compile "org.springframework.data:spring-data-mongodb:1.0.1.RELEASE"
		compile "org.mongodb:mongo-java-driver:2.7.3"

		// Servlet Api
		providedCompile "javax.servlet:servlet-api:2.5"
		compile "javax.servlet:jstl:1.2"

		testCompile "org.springframework:spring-test-mvc:1.0.0.BUILD-SNAPSHOT"
	
		// Springockito
		testCompile "org.kubek2k:springockito:1.0.4",
			"org.kubek2k:springockito-annotations:1.0.2"
	}
}

// javadoc 
task projectDoc(type: Javadoc, group: 'Documentation', description: 'Generates aggregated Javadoc API documentation.') {
	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.header = rootProject.description
	source subprojects.collect { subproj ->
		subproj.sourceSets.main.allJava
	}
	destinationDir = new File(buildDir, "docs")
	classpath = files(subprojects.collect { project ->
		project.sourceSets.main.compileClasspath
	})
}

// wrapper task to create the download script
task wrapper(type: Wrapper, group: 'Gradle wrapper', description: 'Gradle will be automatically downloaded and used to run the build.') {
	gradleVersion = '1.0'
}

